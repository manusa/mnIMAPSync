buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.sonarqube' version '2.7'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

dependencies {
    compile('com.sun.mail:javax.mail:1.6.1')
    testCompile('org.junit.jupiter:junit-jupiter:5.5.1')
    testCompile('org.mockito:mockito-core:3.0.0')
    testCompile('org.hamcrest:java-hamcrest:2.0.0.0')
    testCompile('org.jmockit:jmockit:1.47')
}

group = 'com.marcnuri'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
sourceCompatibility = 1.8

mainClassName = 'com.marcnuri.mnimapsync.MNIMAPSync'

test {
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task fatJar(type: Jar, group: BasePlugin.BUILD_GROUP) {
    manifest {
        attributes (
            'Main-Class': mainClassName,
            'Class-Path': configurations.runtime.files.collect { "$it.name" }.join(' ')
        )
    }
    archiveBaseName = project.name
    archiveVersion = ''
    archiveClassifier = 'all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
